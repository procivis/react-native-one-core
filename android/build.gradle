import java.security.MessageDigest
class Checksum {
    static getChecksum(file, type) {
        def digest = MessageDigest.getInstance(type)
        def inputStream = file.newInputStream()
        def buffer = new byte[16384]
        def len

        while((len=inputStream.read(buffer)) > 0) {
            digest.update(buffer, 0, len)
        }

        def result = ""
        for(byte b : digest.digest()) {
            result += toHex(b)
        }
        return result
    }
    private static hexChr(int b) {
        return Integer.toHexString(b & 0xF)
    }
    private static toHex(int b) {
        return hexChr((b & 0xF0) >> 4) + hexChr(b & 0x0F)
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 33)
    buildToolsVersion = "30.0.2"

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 33)
        versionCode 1
        versionName "1.0.0"
        
        consumerProguardFiles 'proguard-rules.pro'
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:${safeExtGet('kotlinVersion', '1.8.0')}")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1")
    implementation 'com.facebook.react:react-native:+'

    releaseImplementation(name: 'onecore-release', ext: 'aar')
    debugImplementation(name: 'onecore-debug', ext: 'aar')
    implementation 'net.java.dev.jna:jna:5.13.0@aar'
    implementation("org.jetbrains.kotlin:kotlin-reflect:${safeExtGet('kotlinVersion', '1.8.0')}")
}

def downloadFile = { url, file ->
    new URL(url).withInputStream { downloadStream ->
        file.withOutputStream { fileOut ->
            fileOut << downloadStream
        }
    }
}

def coreUrlAAR = { buildType ->
    def name = "onecore-${buildType}"
    def coreSdkJsonFile = file("$projectDir/../core-sdk.json")
    def coreSdkJson = new groovy.json.JsonSlurper().parseText(coreSdkJsonFile.text)
    def sdk = coreSdkJson.android[buildType]

    def url = sdk.url
    File file = new File("$projectDir/libs/${name}.aar")
    file.parentFile.mkdirs()

    def sha1Expected = sdk.sha1
    if (file.exists()) {
        if (Checksum.getChecksum(file, "SHA1") == sha1Expected) {
            // all good
            return
        }
        // The file is most likely just stale -> delete and download again
        file.delete()
    }

    downloadFile(url, file)
    def sha1sum = Checksum.getChecksum(file, "SHA1")
    if (sha1sum != sha1Expected) {
        // if the file hash still does not match then something is off
        println "URL($name): $url"
        println "sha1 expected: $sha1Expected"
        println "sha1 actual: $sha1sum"
        throw new Error("AAR file SHA1 mismatch: " + name)
    }
}

afterEvaluate {
    coreUrlAAR('debug')
    coreUrlAAR('release')
}